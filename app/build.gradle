apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def superpowered_sdk_path = properties.getProperty('superpowered.dir')
def instabug_token = properties.getProperty('instabug.token')

apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}
android {
    flavorDimensions "default"

    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'

    defaultConfig {
        applicationId "com.juztoss.rhythmo"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 33
        versionName "1.0.16"

        manifestPlaceholders = [INSTABUG_TOKEN: "${instabug_token}"]

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' // these platforms cover 99% percent of all Android devices
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_ARM_NEON=TRUE', '-DANDROID_STL=c++_static', "-DPATH_TO_SUPERPOWERED:STRING=${superpowered_sdk_path}"
                cFlags '-O3', '-fsigned-char' // full optimization, char data type is signed
                cppFlags '-fsigned-char', "-I${superpowered_sdk_path}"
            }
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni']
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles.add(file("proguard-rules.pro"))
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    productFlavors {
        create("dev") {
            applicationId "com.juztoss.rhythmo.dev"
        }
        create("prod") {
            applicationId "com.juztoss.rhythmo"
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    annotationProcessor ('com.jakewharton:butterknife-compiler:8.5.1')
    implementation fileTree(include: ['*.jar'], dir: 'libs/')
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation ('com.jakewharton:butterknife:8.5.1')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.11'
    implementation 'de.psdev.licensesdialog:library:1.8.2-SNAPSHOT'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2')
    {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2.2')
    {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'junit:junit:4.12'

    implementation 'com.instabug.library:instabug:4.0.7'
}