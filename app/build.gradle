apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def superpowered_sdk_path = properties.getProperty('superpowered.dir')
def essentia_sdk_path = properties.getProperty('essentia.dir')

apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.5.0'
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}
android {
    compileSdkVersion = 25
    buildToolsVersion = "25.0.1"

    defaultConfig {
        applicationId "com.juztoss.rhythmo"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 31
        versionName "1.0.14"

        ndk {
            abiFilters 'arm64-v8a'
//            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' // these platforms cover 99% percent of all Android devices
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16',
                        '-DANDROID_TOOLCHAIN=clang',
                        '-DANDROID_ARM_NEON=TRUE',
                        '-DANDROID_STL=gnustl_static',
                        "-DPATH_TO_SUPERPOWERED:STRING=${superpowered_sdk_path}",
                        "-DPATH_TO_ESSENTIA:STRING=${essentia_sdk_path}"
                cFlags '-O3', '-fsigned-char' // full optimization, char data type is signed
                cppFlags '-fsigned-char', "-fexceptions", "-frtti"
            }
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni']
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles.add(file("proguard-rules.pro"))
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    productFlavors {
        create("dev") {
            applicationId "com.juztoss.rhythmo.dev"
        }
        create("prod") {
            applicationId "com.juztoss.rhythmo"
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    annotationProcessor ('com.jakewharton:butterknife-compiler:8.5.1')
    compile fileTree(include: ['*.jar'], dir: 'libs/')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-annotations:25.1.0'
    compile ('com.jakewharton:butterknife:8.5.1')
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.11'
    compile 'de.psdev.licensesdialog:library:1.8.2-SNAPSHOT'
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2')
    {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2')
    {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'junit:junit:4.12'
}